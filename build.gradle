buildscript {
    repositories {
        //new forge repository
        maven { url "https://maven.minecraftforge.net" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
    }
}

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.0"
group = "dreamys.studio" //http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "examplemod" //name of the output jar

sourceCompatibility = targetCompatibility = 1.8 //using java 8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22" //mappings for 1.8.9
    makeObfSourceJar = false //disable creation of sources jar
}

configurations {
    shade
    compile.extendsFrom(shade)
}

repositories {
    //repos for libraries (not gradle plugins)
    //example:
    //maven { url "https://repo.hypixel.net/repository/Hypixel" }
}

dependencies {
    //shade will bundle your library to the output jar
    //your libraries will be accessible outside of intellij
    //example:
    //shade "net.hypixel:hypixel-api-transport-apache:4.1"

    //compile will make your library available only in this dev environment
    //your libraries wont be accessible outside of intellij
    //example:
    //compile "net.hypixel:hypixel-api-transport-apache:4.1"
}

processResources {
    //this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    //replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        //replace version and mcversion
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    //copy everything else, that"s not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

shadowJar {
    //go over each dependency that uses "shade" configuration and include it in the shadow jar
    dependencies {
        project.configurations.shade.resolvedConfiguration.getFirstLevelModuleDependencies().each {
            include(dependency(it.toString().replace(";default", "")))
        }
    }
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    classifier "" //prevent creation of unshadowed jar
}

//reobf will throw error if 0 shadowed libraries
if (project.configurations.shade.resolvedConfiguration.getFirstLevelModuleDependencies().size() > 0)
reobf {
    //reobfuscate the shadowed jar
    shadowJar {} 
}